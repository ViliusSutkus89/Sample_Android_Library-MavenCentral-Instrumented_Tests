plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.viliussutkus89'

android {
    compileSdkVersion 32
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 32

        versionCode 12452
        versionName rootProject.version

        archivesBaseName = rootProject.name

        consumerProguardFiles 'proguard-rules.pro'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Following Config Fields used in VersionGetter,
        // Not important for the library template
        buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
        buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
        buildConfigField("String", "GIT_COMMMIT", "\"${System.env.GITHUB_SHA}\"")
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId rootProject.name
                version = rootProject.version

                pom {
                    name = rootProject.name
                    description = 'Sample_Android_Library-MavenCentral-Instrumented_Tests'
                    url = 'https://github.com/ViliusSutkus89/Sample_Android_Library-MavenCentral-Instrumented_Tests'
                    packaging = 'aar'
                    inceptionYear = '2021'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = "ViliusSutkus89"
                            name = "Vilius Sutkus"
                            email = "ViliusSutkus89@gmail.com"
                        }
                    }
                    scm {
                        url = 'https://github.com/ViliusSutkus89/Sample_Android_Library-MavenCentral-Instrumented_Tests'
                        connection = 'https://github.com/ViliusSutkus89/Sample_Android_Library-MavenCentral-Instrumented_Tests.git'
                    }
                }
            }
        }
    }

    // This may look ugly...
    // but useInMemoryPgpKeys expects valid keys even with required { false }
    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}
